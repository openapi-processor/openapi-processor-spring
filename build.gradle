plugins {
    id 'jacoco'
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id "org.sonarqube" version "3.0"
    id 'org.jetbrains.dokka' version '1.4.10'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'org.unbroken-dome.test-sets' version '3.0.1'
    id "com.github.ben-manes.versions" version "0.33.0"
}

group projectGroupId
version projectVersion

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}


ext {
    processorApiVersion = '1.1.0'
    jacksonVersion = '2.11.3'

    bintrayUser = project.findProperty ('BINTRAY_USER') ?: System.getenv ("BINTRAY_USER") ?: 'n/a'
    bintrayKey =  project.findProperty ('BINTRAY_KEY') ?: System.getenv ("BINTRAY_KEY") ?: 'n/a'
}


repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }

    maven {
        url "https://dl.bintray.com/openapi-processor/primary"
        content {
           includeGroup "io.openapiprocessor"
        }
        mavenContent {
            releasesOnly()
        }
    }

    maven {
        url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
        content {
           includeGroup "io.openapiprocessor"
        }
        mavenContent {
            snapshotsOnly()
        }
    }
}

sourceSets {
  main {
    java {
        srcDirs "${buildDir}/version"
    }
  }
}

compileKotlin.dependsOn "generateVersion"


test {
    useJUnitPlatform()
}

testSets {
    testInt
}

check.dependsOn testInt

compileTestGroovy {
    dependsOn 'compileKotlin'
    classpath += files(compileKotlin.destinationDir)
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    components.all(JacksonPlatformRule)
    components.all(KotlinPlatformRule)

    compileOnly "io.openapiprocessor:openapi-processor-api:$processorApiVersion"
    implementation 'io.openapiprocessor:openapi-processor-core:1.3.0'

    implementation 'org.slf4j:slf4j-api:1.7.30'

    testImplementation ("io.openapiprocessor:openapi-processor-api:$processorApiVersion")

    testImplementation ('io.openapiprocessor:openapi-processor-test:1.0.1') {
        exclude group: 'com.google.guava'
    }

    testImplementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    testImplementation("com.google.jimfs:jimfs:1.1") {
         exclude group: 'com.google.guava'
     }

    testImplementation 'org.codehaus.groovy:groovy:2.5.13'
    testImplementation ('org.spockframework:spock-core:2.0-M3-groovy-2.5') {
        exclude group: 'org.codehaus.groovy' // avoid unused groovy packages
    }
    testImplementation ('org.spockframework:spock-junit4:2.0-M3-groovy-2.5') {
        exclude group: 'org.codehaus.groovy' // avoid unused groovy packages
    }
    testImplementation("io.mockk:mockk:1.10.2")
    testImplementation("io.kotest:kotest-runner-junit5:4.2.5")
    testImplementation 'net.bytebuddy:byte-buddy:1.10.16'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'

    constraints {
        testImplementation ('org.junit.platform:junit-platform-engine:1.6.2')
        testImplementation ('junit:junit:4.13')
    }
}

tasks.withType(Test) {
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }

    getExecutionData().setFrom(fileTree(buildDir).include("/jacoco/*.exec"))
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set ('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: [dokkaHtml]) {
    archiveClassifier.set ('javadoc')
    from "$buildDir/docs"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dokkaHtml {
    outputDirectory = file("$buildDir/docs/kotlin".toString ())
}

sonarqube {
  properties {
    property "sonar.projectKey", "openapi-processor_openapi-processor-spring"
    property "sonar.organization", "openapi-processor"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.coverage.jacoco.xmlReportPaths", "$buildDir/reports/jacoco/test/jacocoTestReport.xml"
  }
}

apply plugin: VersionPlugin
apply from: "${rootProject.rootDir}/gradle/publishing.gradle"
apply from: "${rootProject.rootDir}/gradle/publishing.tasks.gradle.kts"
