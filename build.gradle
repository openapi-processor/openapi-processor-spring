plugins {
    id 'groovy'
    id 'java-library'
    id 'org.unbroken-dome.test-sets' version '2.1.1'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
}

group 'com.github.hauner.openapi'
version '1.0.0.A2'

targetCompatibility = JavaVersion.VERSION_1_8

ext {
    generatrApiVersion = '1.0.0.B1'

    bintrayUser = project.hasProperty ('BINTRAY_USER') ? BINTRAY_USER : 'n/a'
    bintrayKey =  project.hasProperty ('BINTRAY_KEY')  ? BINTRAY_KEY  : 'n/a'
}


repositories {
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/hauner/openapi-generatr"
    }
}

testSets {
    testInt
}

check.dependsOn testInt

dependencies {
    implementation 'org.codehaus.groovy:groovy:2.5.4'
    implementation 'io.swagger.parser.v3:swagger-parser:2.0.12'
    implementation 'org.yaml:snakeyaml:1.25'
    compileOnly "com.github.hauner.openapi:openapi-generatr-api:$generatrApiVersion"

    testImplementation 'net.bytebuddy:byte-buddy:1.9.13'
    testImplementation ('org.spockframework:spock-core:1.3-groovy-2.5') {
        // avoid conflicts with explicit groovy version
        exclude group: 'org.codehaus.groovy'
    }

    testIntImplementation "com.github.hauner.openapi:openapi-generatr-api:$generatrApiVersion"
    testIntImplementation 'io.github.java-diff-utils:java-diff-utils:4.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier = 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['GeneratrSpring']

    pkg {
        repo = 'openapi-generatr'
        name = 'openapi-generatr-spring'
        //userOrg = 'openapi-generatr'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/hauner/openapi-generatr-spring'

        version {
            name = project.version
        }
    }
}

publishing {
    publications {
        GeneratrSpring (MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}
